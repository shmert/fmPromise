/**
 * This custom function is responsible for building the URL of an fmPromise web viewer.
 *
 * When a brand new fmPromise web viewer is dragged to a layout, this displays the `fm-promise-select-page.html` module.
 * Otherwise, if $$FMPROMISE_DEVMODE is true, the configured filename is read from the $DOCUMENTS/fmPromise/{{filename}} directory
 * If $$FMPROMISE_DEVMODE is false, the configured module is read from the `fmPromiseModule::sourceMinified` field
 */
Let (
  [
    leading = "'data:text/html,'" ;
    trailing = "'<script>FMPROMISE_WEB_VIEWER_NAME=''" & webViewerName & "'';</script>'" ;
    configured = ExecuteSQLe ( "select " & leading & ", m.sourceMinified, " & trailing & " from fmPromiseWebViewer v join fmPromiseModule m on v.moduleId=m.id where v.id=?" ; ¶ ; ¶ ; webViewerName ) ;
    filename = ExecuteSQLe ( "select filename from fmPromiseWebViewer v join fmPromiseModule m on v.moduleId=m.id where v.id=?" ; "" ; "" ; webViewerName )
  ] ;
  Case (
   ( $$FMPROMISE_DEVMODE ) and not IsEmpty ( configured ) ;
      // read the configured module content directly from the fmPromiseServer
      _fmPromiseServerAddress & "/build/" & filename & "?webViewerName=" & GetAsURLEncoded ( webViewerName ) & "&liveReload=true" ;
    not IsEmpty ( configured ) ;
      // return the configured minified contents in production mode
      configured ;
    // otherwise, prompt for a file name
    ExecuteSQLe ( "select " & leading & ", m.sourceMinified, " & trailing & " from fmPromiseModule m where m.filename=?" ; ¶ ; ¶ ; "fm-promise-built-in/fm-promise-select-page.html" )
  )
)
